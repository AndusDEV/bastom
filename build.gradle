buildscript {
    repositories {
        maven { url 'https://repo.spongepowered.org/maven' }
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'net.kyori.blossom' version '1.3.0'
    id 'java'
}

def displayName = "MicroStom"
def minestomVersion = "e4a141be31913a9a227914d375dd04e4a18e9bc1"

group 'com.github.klainstom'
version '2.0'

repositories {
    mavenCentral()
    maven { url 'https://repo.spongepowered.org/maven' }
    maven { url 'https://jitpack.io' }
}

dependencies {
    implementation 'com.github.Minestom:Minestom:' + minestomVersion
}

blossom {
    replaceToken '$Name', displayName
    replaceToken '$name', displayName.toLowerCase()
    replaceToken '$version', version
    replaceToken '$minestomVersion', minestomVersion
}

processResources.expand(
    'Name': displayName,
    'version': version
)

shadowJar {
    manifest.attributes 'Main-Class': 'com.github.klainstom.Main'
    archiveBaseName.set(displayName)
    archiveClassifier.set('')
    archiveVersion.set(project.version.toString())
}

test {
    useJUnitPlatform()
}

build {
    dependsOn(shadowJar)
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}